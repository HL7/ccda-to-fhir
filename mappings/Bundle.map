map "http://hl7.org/fhir/cda/mapping/BundleToCDA" = "FHIR Bundle to CDA"

uses "http://hl7.org/fhir/cda/StructureDefinition/ClinicalDocument" alias ClinicalDocument as target
uses "http://hl7.org/fhir/StructureDefinition/Bundle" alias Bundle as source
uses "http://hl7.org/fhir/StructureDefinition/Composition" alias Composition as source

imports "http://hl7.org/fhir/cda/mapping/fhirToCDATypes"

group BundleClinicalDocument(source source : Bundle, target target : ClinicalDocument) {
  source.identifier -> target.id "id";
  source.entry as entry then {
    entry.resource as resource where resource.ofType(FHIR.Composition) then CompositionClinicalDocument(source, resource, target);
  };
}

group CompositionClinicalDocument(source bundle : Bundle, source source : Composition, target target : ClinicalDocument) {
  source.language -> target.languageCode "languageCode";
  source.extension as extension where extension.url = 'http://hl7.org/fhir/cda/StructureDefinition/templateID' then {
    extension.valueIdentifier -> target.templateId "identifier";
  } "templateID";
  source.identifier -> target.setId "setID";
  source.type -> target.code "code";
  source.date -> target.effectiveTime "effectiveTime";
  source.title -> target.title;
  source.confidentiality -> target.confidentialityCode "confidentialityCode";
  source.event as srcEvent ->  target.documentationOf as doc,  doc.serviceEvent as event then {
    srcEvent.period -> event.effectiveTime;
  };
  source.section as section ->  target.component as comp,  comp.structuredBody as body,  body.component as secComp,  secComp.section as tgtSection then {
    section.extension as extension where extension.url = 'http://hl7.org/fhir/cda/StructureDefinition/templateID' then {
      extension.valueIdentifier -> tgtSection.templateId "identifier";
    } "templateID";
    section.code -> section.code;
    section.title -> tgtSection.title;
    section.text as srcText -> tgtSection.text as tgtText then {
      srcText.div -> tgtText;
    };
  } "sections";
}

